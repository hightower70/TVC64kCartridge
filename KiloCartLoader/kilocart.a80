        .include "definitions.a80"

        org     CART_START_ADDRESS
        db      "MOPS"

        ;---------------------------------------------------------------------
        ; Main program entry point. Intializes cart program
ENTRY:
        ld      a, (PAGE0_SELECT)               ; Change to Page0
        jp      MAIN                            ; Jump to initialization

        db      "KILOCART"

        db      SW_MAJOR_VERSION                ; Major version number
        db      SW_MINOR_VERSION                ; Minor version number

        ; Initialize cart program
MAIN:
        ; Set memory map to: U0, U1, U2, CART
        ld      a, P_U0_U1_U2_CART
        ld      (P_SAVE), a
        out     (PAGE_REG), a

        push    de                              ; save returning address for 2.x ROM

        ; Copy BASINIT program to the RAM
        ld	hl, RAM_FUNCTIONS_STORAGE
	ld	de, RAM_FUNCTIONS
	ld	bc, RAM_FUNCTIONS_CODE_LENGTH
	ldir

	; call BASIC area initialize and version detection
	call	BASIC_INITIALIZE

        ; calculate returning address for 2.x ROM
        pop     de                              ; restore returning address for 2.x ROM
	ld	hl, -22
	add     hl, de
	ld      (ROM_RETURN_ADDRESS), hl

        ; *** Chain system function call

        ; Copy system function pass (return to original ROM function) code to RAM
        ld      HL, SYSTEM_FUNCTION_PASS_STORAGE
        ld      DE, SYSTEM_FUNCTION_PASS
        ld      BC, SYSTEM_FUNCTION_PASS_CODE_LENGTH
        ldir

        ; Copy ROM entry address of the system function call
        ld      HL, (SYSTEM_FUNCTION_ROM_ENTRY_ADDRESS)
        ld      (SYSTEM_FUNCTION_CHAIN_ADDRESS+1), HL

        ; Overwrite system function caller RAM code
        ld      hl, SYSTEM_FUNCTION_CALLER_STORAGE
        ld      de, SYSTEM_FUNCTION_CALLER
        ld      bc, SYSTEM_FUNCTION_CALLER_CODE_LENGTH
        ldir

        ; No ROM file is opened
        ld      a, 0
        ld      (FILE_OPENED_FLAG), a

        ; *** Load and Start the first program from the ROM filesystem ***
        call    GET_FILE_SYSTEM_INFO

        ld      de, CART_MAX_FILENAME_LENGTH    ; Skip filename
        add     hl, de
        ld      e,  (hl)                        ; Load address
        inc     hl
        ld      d,  (hl)
        inc     hl
        ld      c,  (hl)                        ; Load length
        inc     hl
        ld      b,  (hl)
        
        ld      l, e
        ld      h, d
	ld	de, BASIC_PROGRAM_START

	call    COPY_PROGRAM_TO_RAM

	; setup BASIC program location
        ld	hl, BASIC_PROGRAM_START
	ld	(1720h), hl
	ld	(1722h), hl

	; Start program
	ld	a,(VERSION)
	or      a
	jp	z, BASIC_RUN_1x

        ; Start program routine for 2.x version rom	
        ; set BASIC flag to no start screen, no prompt, no new command and autostart
        ld	a, 0fh
        ld	(BASIC_FLAG), a

	; load return address
	ld	hl,(ROM_RETURN_ADDRESS)
				
	; Set memory map to: SYS, U1, U2, CART
	ld	a, P_SYS_U1_U2_CART
        ld      (P_SAVE), A
        out     (PAGE_REG), A

	; return back to ROM
	jp	(hl)        

        ;------------------------------------
        ; Load file system area area address
        ; Input: -
        ; Output: HL - File system area address depending on the basic version
        ;          A - Number of files in the file system
        ; Destroys: A, F
GET_FILE_SYSTEM_INFO:
        ld      a, (VERSION)
        or      a
        jr      z, SET_VERSION1x_FILE_SYSTEM
        
        ld      hl, (FILE_SYSTEM.DIRECTORY2x_ADDRESS)       ; File system for 2.x ROM
        
        ld      a, high(CART_START_ADDRESS)                 ; Convert ROM address to CART address
        or      h
        ld      h, a        

        ld      a, (FILE_SYSTEM.FILES2x_COUNT)              ; Get file count
        ret

SET_VERSION1x_FILE_SYSTEM:        
        ld      hl, (FILE_SYSTEM.DIRECTORY1x_ADDRESS)       ; File system for 1.x ROM

        ld      a, high(CART_START_ADDRESS)                 ; Convert ROM address to CART address
        or      h
        ld      h, a        

        ld      a, (FILE_SYSTEM.FILES1x_COUNT)              ; Get file count
        ret

       ;---------------------------------------------------------------------
       ; System function handler
SYSTEM_FUNCTION:
        ex      af,af'                      ; Get function code to AF
        push    af                          ; Save it on stack

        and     SYSTEM_FUNCTION_CLASS_MASK  ; Check if it is cassette class
        cp      CAS_FUNCTION_CLASS
        jp      z, HANDLE_CAS_FUNCTIONS

        pop     af                          ; Not cassette function, restore function code

NOT_KNOWN_CAS_FUNCTION:
        ex      af,af'                      ; Restore function code to AF'
        jp      SYSTEM_FUNCTION_PASS        ; and continus with the original ROM routine

HANDLE_CAS_FUNCTIONS:
        pop     af                          ; Restore function code
        push    hl                          ; Save HL

        ; Determine CAS function
        cp      a, CAS_FN_OPEN
        jp      z, CAS_OPEN

        cp      a, CAS_FN_CHIN
        jp      z, CAS_CH_IN

        cp      a, CAS_FN_BKIN
        jp      z, CAS_BKIN

        cp      a, CAS_FN_CLOSE_RD
        jp      z, CAS_CLOSE_RD

        pop     hl                          ; Restore HL
        jp      NOT_KNOWN_CAS_FUNCTION

        ;*********************************************************************
        ;* Cassette Functions
        ;*********************************************************************

        ;---------------------------------------------------------------------
        ; Casette: Open file
        ; Input:  DE - file name pointer
        ; Output: A - status code
        ;         DE - file name pointer
CAS_OPEN:
        ; Check if ROM file is already opened
        ld      a, (FILE_OPENED_FLAG)
        or      a
        jr      z, CHECK_FILE_NAME

        ; file already opened
        ld      a, CAS_ERR_ALREADY_OPENED
        jp      CAS_RETURN

CHECK_FILE_NAME:
        ld      l, e                        ; HL = File name pointer
        ld      h, d
        push    de                          ; save file name pointer
        ld      de, FILE_NAME_BUFFER        ; temporary file name storage

        ; Convert file name to uppercase
        ld      a,  (hl)                    ; load file name length
        ld      (FILE_NAME_LENGTH), a       ; save file name length
        ld      b, a                        ; b = file name length
        inc     hl

FILENAME_TOUPPER_LOOP:
        ld      a,  (hl)                    ; load character
        cp      a, 'a'
        jr      c, STORE_FILENAME_CHARACTER
        cp      a, 'z' + 1
        jr      nc, FILENAME_CHECK_ACCENTED_CHARACTERS
        and     $df                         ; letter to upercase
        jr      STORE_FILENAME_CHARACTER

FILENAME_CHECK_ACCENTED_CHARACTERS:        
        cp      a, $90                      ; accented letters
        jr      c, STORE_FILENAME_CHARACTER
        cp      a, $99
        jr      nc, STORE_FILENAME_CHARACTER
        sub     a, $10

STORE_FILENAME_CHARACTER:
        ld      (de), a
        inc     de
        inc     hl
        djnz    FILENAME_TOUPPER_LOOP

        ; append '.CAS' if there is no extension
        ld      a, (FILE_NAME_LENGTH)
        ld      b, a
        ld      hl, FILE_NAME_BUFFER

FIND_EXTENSION:
        ld      a, (hl)
        inc     hl
        cp      a, '.'
        jr      z, FIND_FILE_NAME

        djnz    FIND_EXTENSION

        ; no extension, append '.CAS' if buffer is long enough
        ld      a, (FILE_NAME_LENGTH)
        cp      a, CART_MAX_FILENAME_LENGTH - CAS_EXTENSION_LENGTH + 1
        jr      nc, FIND_FILE_NAME

        ; append extension
        ld      e, a
        add     a, CAS_EXTENSION_LENGTH
        ld      (FILE_NAME_LENGTH), a       ; Update file name length

        ld      a, low(FILE_NAME_BUFFER)    ; Determine append position
        add     a, e
        ld      e, a
        ld      a, 0
        adc     a, high(FILE_NAME_BUFFER)
        ld      d, a

        ld      hl, CAS_EXTENSION
        ld      bc, CAS_EXTENSION_LENGTH
        ldir                                ; Append extension

FIND_FILE_NAME:
        ; Find filename in the ROM file system
        call    GET_FILE_SYSTEM_INFO         ; Get file system address and number of files
        ld      c, a                         ; Number of files in the file system

COMPARE_FILE_NAMES:
        push    hl                           ; save file system entry address

        ; get file name length to B and file name buffer to DE
        ld      a, (FILE_NAME_LENGTH)
        ld      b, a
        ld      de, FILE_NAME_BUFFER

COMPARE_FILENAME_CHARACTERS:
        ld      a, (de)                      ; Compare file name characters
        cp      (hl)

        jr      nz, CHECK_NEXT_FILE          ; non matching -> next file

        inc     hl
        inc     de

        djnz    COMPARE_FILENAME_CHARACTERS

        ; file found
        ld      a, 1                         ; set file opened flag
        ld      (FILE_OPENED_FLAG), a

        ; Copy RAM code
        ld	hl, RAM_FUNCTIONS_STORAGE
	ld	de, RAM_FUNCTIONS
	ld	bc, RAM_FUNCTIONS_CODE_LENGTH
        ldir
        
        ; get address and length
        pop     bc                           ; Restore file system entry address
        ld      hl,  FileSystemEntry.FILE_ADDRESS
        add     hl, bc

        ; store file address in CURRENT_FILE_ADDRESS
        ld      a, (hl)
        ld      (CURRENT_FILE_ADDRESS), a
        inc     hl
        ld      a, (hl)
        ld      (CURRENT_FILE_ADDRESS+1), a
        inc     hl

        ; store file length in CURRENT_FILE_LENGTH and CAS header
        ld      a, (hl)
        ld      (CURRENT_FILE_LENGTH), a
        ld      (CAS_HEADER.FileLength), a
        inc     hl
        ld      a, (hl)
        ld      (CURRENT_FILE_LENGTH+1), a
        ld      (CAS_HEADER.FileLength+1), a

        xor     a                           
        ld      (CURRENT_CAS_HEADER_POS), a ; reset CAS header pos for CH_IN
        ld      (BUFFER), a                 ; Set non-buffered file

        pop     de                          ; restore file name pointer
        ld      de, FILE_NAME_LENGTH        ; return back the modified file name (FILE_NAME_BUFFER) and length (FILE_NAME_LENGTH)

        xor     a                           ; no error
        jp      CAS_RETURN
        
CHECK_NEXT_FILE:
        pop     hl                          ; file system entry address
        ld      de, FileSystemEntry         ; next entry
        add     hl, de

        dec     c
        ld      a,  c
        or      a
        jr      nz, COMPARE_FILE_NAMES

        ; file not found
        pop     de                          ; restore file name pointer

RET_NO_OPEN_FILE_ERROR:
        ld      a, CAS_ERR_NO_OPEN_FILE
        jp      CAS_RETURN

        ;---------------------------------------------------------------------
        ; Casette: Character input
        ; Input: -
        ; Output: C - character
        ;         A - status code
CAS_CH_IN:
        ; check if file is opened
        ld      a, (FILE_OPENED_FLAG)
        or      a
        jr      z, RET_NO_OPEN_FILE_ERROR

        ; load file position
        ld      a,(CURRENT_CAS_HEADER_POS)
        cp      CASHeader
        jr      nc, CAS_CH_IN_EOF

        ld      hl, CAS_HEADER              ; Calculate CAS header address (A+CAS_HEADER)
        add     a, l                        ; A = A+L
        ld      l, a                        ; L = A+L
        adc     a, h                        ; A = A+L+H+carry
        sub     l                           ; A = H+carry
        ld      h, a                        ; H = H+carry

        ld      c,  (hl)                    ; Load CAS header data

        ld      a, (CURRENT_CAS_HEADER_POS) ; Increment CAS pointer
        inc     a
        ld      (CURRENT_CAS_HEADER_POS), a

        xor     a                           ; success
        jp      CAS_RETURN

CAS_CH_IN_EOF:
        ld      a, CAS_ERR_EOF        
        jp      CAS_RETURN

        ;---------------------------------------------------------------------
        ; Casette: Block input
        ; Input: DE - Buffer address
        ;        BC - Length of the buffer
        ; Output: A - status code
CAS_BKIN:
        ; check if file is opened
        ld      a, (FILE_OPENED_FLAG)
        or      a
        jr      z, RET_NO_OPEN_FILE_ERROR

        ; Check remaining file length
        ld      hl, (CURRENT_FILE_LENGTH)
        ld      a, l
        or      h
        jr      z, CAS_BKIN_EOF

        or      a
        sbc     hl, bc
        jr      nc, CAS_BKIN_LOAD

        ; requested length is longer than file length, adjust it
        ld      bc, (CURRENT_FILE_LENGTH)   ; Bytes to copy
        ld      hl, 0                       ; No more remaining bytes

CAS_BKIN_LOAD:
        ld      (CURRENT_FILE_LENGTH), hl   ; Update remaining length

        ld      hl, (CURRENT_FILE_ADDRESS)  ; load file address
        call    COPY_PROGRAM_TO_RAM
        ld      (CURRENT_FILE_ADDRESS), hl  ; Update address

        xor     a                           ; Success
        jp      CAS_RETURN

CAS_BKIN_EOF:
        ld      a, CAS_ERR_EOF              ; End of file
        jp      CAS_RETURN

        ;---------------------------------------------------------------------
        ; Casette: Close file (read mode)
        ; Input: -
        ; Output: A - status code
CAS_CLOSE_RD:
        xor     a

        ; reset file address
        ld      (CURRENT_FILE_ADDRESS), a
        ld      (CURRENT_FILE_ADDRESS+1), a

        ; reset file length
        ld      (CURRENT_FILE_LENGTH), a
        ld      (CAS_HEADER.FileLength), a
        ld      (CURRENT_FILE_LENGTH+1), a
        ld      (CAS_HEADER.FileLength+1), a 

        ; reset file opened
        ld      (FILE_OPENED_FLAG), a      

        jp      CAS_RETURN

        ;---------------------------------------------------------------------
        ; Returns from CAS function
CAS_RETURN:
        pop hl                                          ; Restore HL
        or a                                            ; Set flags according to error code
        jp SYSTEM_FUNCTION_RETURN


CAS_EXTENSION:
        db      ".CAS"
CAS_EXTENSION_LENGTH equ $-CAS_EXTENSION


        ; *** Other modules ***
        include "ramfunctions.a80"

        ; *** File system data ***
FILE_SYSTEM FileSystemStruct